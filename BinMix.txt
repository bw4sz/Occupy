
    model {
    
    # Priors
    alpha.lam ~ dnorm(0, 0.1)
    beta1.lam ~ dnorm(0, 0.1)
    beta2.lam ~ dnorm(0, 0.1)
    alpha.p ~ dnorm(0, 0.1)
    beta.p ~ dnorm(0, 0.1)
    
    # Likelihood
    # Biological model for true abundance
    for (i in 1:R) {			# Loop over R sites
    N[i] ~ dpois(lambda[i])
    log(lambda[i]) <- alpha.lam + beta1.lam * vege[i] + beta2.lam * vege2[i]
    }
    
    # Observation model for replicated counts
    for (i in 1:n) {			# Loop over all n observations
    C[i] ~ dbin(p[i], N[site.p[i]])
    logit(p[i]) <- alpha.p + beta.p * vege.p[i]
    }
    
    # Derived quantities
    totalN <- sum(N[])			# Estimate total population size across all sites
    
    }
    
