
    model {
    #Compute intensity for each pair of birds and plants
    for (i in 1:Birds){
    for (j in 1:Plants){
    for (k in 1:Times){
    
    #Process Model
    log(lambda[i,j,k])<-alpha[i] + beta1[i] * Traitmatch[i,j] 
    
    
    #For each camera - there is a latent count
    N[i,j,k] ~ dpois(lambda[i,j,k] * resources[i,j,k] + 0.0000001)
    }
    }
    }
    
    
    #Observed counts for each day of sampling at that camera
    for (x in 1:Nobs){
    
    #Observation Process
    Yobs[x] ~ dbin(detect[Bird[x]],N[Bird[x],Plant[x],Time[x]])    
    
    #Assess Model Fit
    
    #Fit discrepancy statistics
    eval[x]<-detect[Bird[x]]*N[Bird[x],Plant[x],Time[x]] * resources[Bird[x],Plant[x],Time[x]]
    E[x]<-pow((Yobs[x]-eval[x]),2)/(eval[x]+0.5)
    
    ynew[x]~dbin(detect[Bird[x]],N[Bird[x],Plant[x],Time[x]])
    E.new[x]<-pow((ynew[x]-eval[x]),2)/(eval[x]+0.5)
    
    }
    
    for (i in 1:Birds){
    logit(detect[i])<-dcam[i]
    dcam[i] ~ dnorm(dprior,tau_dcam)
    alpha[i] ~ dnorm(alpha_mu,alpha_tau)
    beta1[i] ~ dnorm(beta1_mu,beta1_tau)  
    }
    
    #Hyperpriors
    
    #Detection group prior
    dprior ~ dnorm(0,0.386)
    
    #Group effect detect camera
    tau_dcam ~  dt(0,1,1)I(0,)
    sigma_dcam<-pow(1/tau_dcam,.5)
    
    #Intercept grouping
    alpha_mu~dnorm(0,0.0001)
    
    #Group intercept variance
    alpha_sigma ~ dt(0,1,1)I(0,)
    alpha_tau <- pow(alpha_sigma,-2)
    
    #Trait Slope
    #Mean
    beta1_mu~dnorm(0,0.0001)
    
    #Variance
    beta1_sigma ~ dt(0,1,1)I(0,)
    beta1_tau <- pow(beta1_sigma,-2)
    
    #derived posterior check
    
    fit<-sum(E[]) #Discrepancy for the observed data
    fitnew<-sum(E.new[])
    
    }
    
