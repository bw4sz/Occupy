
    model {
    for (i in 1:Birds){
      for (j in 1:Plants){
      
      # True state model for the only partially observed true state    
      log(lambda[i,j])<-alpha[i] + beta[i] * traitmatch[i,j]
      N[i,j] ~ dpois(lambda[i,j])
      
      for (k in 1:Months) {   

      # Observation model for the actual observations
      Y[i,j,k] ~ dbin(detect[i],N[i,j]) 
      
      #Fit discrepancy statistics
      eval[i,j,k]<-detect[i]*N[i,j]
      E[i,j,k]<-pow((Y[i,j,k]-eval[i,j,k]),2)/(eval[i,j,k]+0.5)

      y.new[i,j,k]~dbin(detect[i],N[i,j])
      E.new[i,j,k]<-pow((y.new[i,j,k]-eval[i,j,k]),2)/(eval[i,j,k]+0.5)
        }
      }
    }
    
    for (i in 1:Birds){
    detect[i] ~ dunif(0,1) # Detection for each bird species
    alpha[i] ~ dnorm(intercept,tau_alpha)
    beta[i] ~ dnorm(gamma,tau_beta)    
    }
    
    #Hyperpriors
    gamma~dnorm(0,0.0001)
    intercept~dnorm(0,0.0001)
    
    tau_alpha ~ dgamma(0.0001,0.0001)
    sigma_int<-pow(1/tau_alpha,0.5) #Derived Quantity
    tau_beta ~ dgamma(0.0001,0.0001)
    sigma_slope<-pow(1/tau_beta,0.5)
    
    
    #derived posterior check
    fit<-sum(E[,,]) #Discrepancy for the observed data
    fitnew<-sum(E.new[,,])
    
    }
    
